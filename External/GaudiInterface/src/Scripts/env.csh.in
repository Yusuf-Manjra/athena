# File:
#   GAUDI_env.csh
#
# Description:
#   GAUDI environment setup file for c shell varients.
#
# Preconditions:
#   The following environment variables are expected to have been set:
#
#   ATLAS_EXTERNAL ATLAS external area (unless GAUDITOP overrides it)
#   ATLAS_SCRIPTS  ATLAS scripts area
#
# Authors:
#   Architecture Team members

# Error handling if ATLAS_SCRIPTS isn't set
if (! $?ATLAS_SCRIPTS ) then
    echo "GAUDI_env.csh: Error - ATLAS_SCRIPTS environment variable not set"
    goto end
endif

# Error handling if OS isn't set
if (! $?OS ) then
    echo "GAUDI_env.csh: Error - OS environment variable not set"
    goto end
endif

# Error handling if SRT_CONF isn't set
if (! $?SRT_CONF ) then
    echo "GAUDI_env.csh: Error - SRT_CONF environment variable not set"
    goto end
endif

# Error handling if SITEROOT doesn't exist. Note that this environment variable
# is needed for the Python setup since that uses a Gaudi setup script.
if (! $?SITEROOT ) then
    setenv SITEROOT /afs/cern.ch
endif
if ( ! -r $SITEROOT ) then
    echo "GAUDI_env.csh: Error - SITEROOT not set and default (/afs/cern.ch) non-existent"
    goto end
endif

# Setup CMT platform configuration
switch ($OS)
    case Solaris*:
        set s_cmtconfig=sun4x_57
        set s_rootconfig=sun4m_53
        set s_cmttag=$s_cmtconfig
        breaksw
    case Linux*:
        set s_cmtconfig=i386_linux22
        set s_rootconfig=$s_cmtconfig
        switch ($SRT_CONF)
            case egcs*:
                set s_cmttag=$s_cmtconfig
                breaksw
            case gcc*:
                set s_cmttag=Linux-2.95.2
                breaksw
            default:
                echo "GAUDI_env.csh: Error - ${OS}/${SRT_CONF} is not supported"
                goto end
        endsw
        breaksw
    default:
        echo "GAUDI_env.csh: Error - ${OS} is not supported"
        goto end
endsw

# Setup GAUDI location
if ( $?GAUDITOP ) then
    set s_gauditop=$GAUDITOP
    echo "GAUDI_env.csh: GAUDITOP overrides the default - set to $GAUDITOP"
else
    # Error handling if ATLAS_EXTERNAL isn't set
    if (! $?ATLAS_EXTERNAL ) then
        echo "GAUDI_env.csh: Error - ATLAS_EXTERNAL environment variable not set"
        goto end
    endif
    set s_gauditop=$ATLAS_EXTERNAL/Gaudi/XXXGAUDI_VERSIONXXX
endif
set s_libs=${s_gauditop}/Athena/XXXATHENA_PACKAGEXXX/${s_cmttag}

# Setup ROOT libraries
if ( $?ROOTTOP ) then
    set s_roottop=$ROOTTOP
    echo "GAUDI_env.csh: ROOTTOP overrides the default - set to $ROOTTOP"
else
    set s_roottop=/afs/cern.ch/na49/library.4/ROOT/v2.25/${s_rootconfig}/root/lib
endif
set s_libs=${s_libs}:${s_roottop}

# Setup the shared library search path
if ( $?LD_LIBRARY_PATH ) then
    setenv LD_LIBRARY_PATH ${s_libs}:${LD_LIBRARY_PATH}
else
    setenv LD_LIBRARY_PATH ${s_libs}
endif

# Setup the required Python search paths. Note that this requires that both
# the CMTPATH and CMTSITE environment variables be setup appropriately.
# In the case of CMTPATH it should be set to the location of the Gaudi
# release (and restored to it's previous value). In the case of CMTSITE
# it is set to the default of ATLAS if it isn't already set.
if ( $?CMTPATH ) then
    set s_oldpath=$CMTPATH
endif
setenv CMTPATH ${s_gauditop}
if ( $?CMTSITE ) then
    set s_oldsite=$CMTSITE
else
    setenv CMTSITE ATLAS
endif
source ${s_gauditop}/SIPython/XXXSIPYTHON_PACKAGEXXX/cmt/setup.csh -tag=$s_cmttag

# LHCXX_DIR is set in ExternalLibs and is not always correct - unset to be safe
unsetenv LHCXX_DIR

if ( $?s_oldpath ) then
    setenv CMTPATH $s_oldpath
else
    unsetenv CMTPATH
endif
if ( ! $?s_oldsite ) then
    unsetenv CMTSITE
endif

# Remove duplicate entries from the library seach path
setenv LD_LIBRARY_PATH `$ATLAS_SCRIPTS/pathclean.pl LD_LIBRARY_PATH`

# Cleanup
end:
unset s_*

