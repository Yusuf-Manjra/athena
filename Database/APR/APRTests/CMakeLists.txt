# Copyright (C) 2002-2021 CERN for the benefit of the ATLAS collaboration

# Declare the package name:
atlas_subdir( APRTests )

# External dependencies:
find_package( CppUnit )
find_package( ROOT )

# Extend the include directories for all components:
include_directories( ${CMAKE_CURRENT_SOURCE_DIR}/TestDictionary )

# Component(s) in the package:
atlas_add_dictionary( test_TestDictionaryDict
   TestDictionary/dict_all.h TestDictionary/classes.xml
   NO_ROOTMAP_MERGE )

# Helper function setting up the tests of the package:
function( _add_test name )
   # Parse the optional argument(s):
   cmake_parse_arguments( ARG "" "DICTHEADER" "DEPENDS" ${ARGN} )
   # Create a dictionary if one was requested:
   if( ARG_DICTHEADER )
      atlas_add_dictionary( test_${name}Dict
         ${ARG_DICTHEADER} ${name}/classes.xml
         INCLUDE_DIRS ${Boost_INCLUDE_DIRS}
         LINK_LIBRARIES ${Boost_LIBRARIES} PersistencySvc
         StorageSvc AthContainers
         NO_ROOTMAP_MERGE )
   endif()
   # Set up the test properties based on the optional argument(s):
   set( properties )
   if( ARG_DEPENDS )
     list( APPEND properties PROPERTIES DEPENDS "${ARG_DEPENDS}" )
   endif()
   # Set up the test:
   atlas_add_test( "${name}_test"
      SOURCES ${name}/*.cpp
      INCLUDE_DIRS ${CPPUNIT_INCLUDE_DIRS}
      LINK_LIBRARIES ${CPPUNIT_LIBRARIES} PersistencySvc TestTools
      StorageSvc AthContainers  FileCatalog
      PersistentDataModel TestTools
      # Need to explicitly link against ROOT libraries to suppress
      # spurious ubsan warnings.
      ${ROOT_LIBRARIES}
      ENVIRONMENT "ROOT_INCLUDE_PATH=$ENV{ROOT_INCLUDE_PATH}:${CMAKE_CURRENT_SOURCE_DIR}/TestDictionary"
      PROPERTIES TIMEOUT 300
      PRE_EXEC_SCRIPT "rm -f pool*.root *xml"
      LOG_IGNORE_PATTERN "POOL_CATALOG is not defined|using default|New file created|FID"
      ${properties} )
endfunction( _add_test )

# Declare the test(s) of the package:
_add_test( FileCatalog_Functionality )
_add_test( StorageSvc_BasicFunctionality )
_add_test( StorageSvc_AuxStore
  DICTHEADER StorageSvc_AuxStore/AuxStoreTest.h
  DEPENDS APRTests_StorageSvc_BasicFunctionality_test_ctest )
_add_test( StorageSvc_MultipleIterators
  DEPENDS APRTests_StorageSvc_AuxStore_test_ctest )
_add_test( StorageSvc_ParallelReadWrite
  DEPENDS APRTests_StorageSvc_MultipleIterators_test_ctest )
_add_test( StorageSvc_TransientMembers
  DICTHEADER StorageSvc_TransientMembers/TestClassWithTransients.h
  DEPENDS APRTests_StorageSvc_ParallelReadWrite_test_ctest )

_add_test( PersistencySvc_Functionality
  DEPENDS APRTests_StorageSvc_TransientMembers_test_ctest )
_add_test( PersistencySvc_FileCatalogOperations
  DEPENDS APRTests_PersistencySvc_Functionality_test_ctest )
_add_test( PersistencySvc_FileOpenWithoutCatalog
  DEPENDS APRTests_PersistencySvc_FileCatalogOperations_test_ctest )
_add_test( PersistencySvc_Parameters
  DEPENDS APRTests_PersistencySvc_FileOpenWithoutCatalog_test_ctest )
_add_test( PersistencySvc_NoClassID
  DICTHEADER PersistencySvc_NoClassID/MyTestClass.h
  DEPENDS APRTests_PersistencySvc_Parameters_test_ctest )

# Clean up:
unset( _add_test )
