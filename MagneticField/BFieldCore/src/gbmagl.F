*
*
*
*

* Author : Saclay Muon Software Group (SaMuSoG)
***************************************************************************
      SUBROUTINE GBMAGL(AVER,RFZSC,II312, BXYZ,
     +NVERSION,BINNER,RMAINN,ZMAINN,RTORI,ZTORI,RTORO,ZTORO,EPS0,
     +SECMAG,NRMAG,NFMAG,NZMAG,RMAG ,FMAG ,ZMAG ,BRMAG,BFMAG,BZMAG,
     +SECFEL,NRR  ,NFF  ,NZZ  ,RRPOS,FFPOS,ZZPOS,TABBR,TABBF,TABBZ,
     +DRPOS,RPOSMI,DFPOS,FPOSMI,DZPOS,ZPOSMI,JRPOS,JFPOS,JZPOS,
     +DRMAG,RMAGMI,RMAGMA,DFMAG,FMAGMI,DZMAG,ZMAGMI,JRMAG,JFMAG,JZMAG)
*
*>> Interpolates tabulated nonlinear 3D ATLAS mag. field BXYZ !
*>> Author : Saclay Muon Software Group (SaMuSoG)
*>> Date   :  8/09/98
*
      IMPLICIT NONE
*
*
      CHARACTER*4 AVER
      INTEGER II312, NVERSION, NRMAG(*),NFMAG,NZMAG, NRR,NFF,NZZ
      INTEGER JRPOS(*),JFPOS(*),JZPOS(*), JRMAG(*),JFMAG(*),JZMAG(*) 
      REAL    RFZSC(6), BXYZ(12), BINNER, RMAINN, ZMAINN
      REAL    RTORI, ZTORI, RTORO, ZTORO, EPS0, SECMAG, SECFEL
      REAL    RMAG(*),FMAG(*),ZMAG(*), BRMAG(*),BFMAG(*),BZMAG(*)
      REAL    RRPOS(*),FFPOS(*),ZZPOS(*), TABBR(*),TABBF(*),TABBZ(*)
      REAL    DRPOS,RPOSMI,DFPOS,FPOSMI,DZPOS,ZPOSMI
      REAL    DRMAG,RMAGMI,RMAGMA,DFMAG,FMAGMI,DZMAG,ZMAGMI
*
      INTEGER I
      REAL    RR,AZ, W, EPSR,EPSZ
      REAL    BXYZMA(12)
*
      real*8  zero,deg,pi
*
      RR = RFZSC(1)
      AZ = RFZSC(6)
      zero = 0
      pi   = 3.141592654
      deg  = 180./pi
*
*>> Inner Part: uniform field if NVERSION=1 or EPS0=0
      IF( RR.LT.RMAINN .AND. AZ.LT.ZMAINN .AND.
     +   (NVERSION.EQ.1.OR.EPS0.GT.0.)) THEN
        DO I=1,II312
          BXYZ(I) = 0.
        ENDDO
        BXYZ(3) =  BINNER
        RETURN
      ENDIF
*
*>> Inner Part: Solenoid field
      IF( RR.LT.RTORI .AND. AZ.LT.ZTORI .AND. NVERSION.GE.2 ) THEN
        CALL BFELIX( SECFEL, NRR,NFF,NZZ, RRPOS,FFPOS,ZZPOS,
     +                  TABBR,TABBF,TABBZ, 
     +DRPOS,RPOSMI,DFPOS,FPOSMI,DZPOS,ZPOSMI,JRPOS,JFPOS,JZPOS,
     +DRMAG,RMAGMI,RMAGMA,DFMAG,FMAGMI,DZMAG,ZMAGMI,JRMAG,JFMAG,JZMAG,
     +                  RFZSC,II312, BXYZ )
        W = 1.
        IF(EPS0.GT.0.) THEN
          EPSR = RR-RMAINN
          EPSZ = AZ-ZMAINN
          IF(0.LT.EPSR.AND.EPSR.LT.EPS0) W=MIN(W,EPSR/EPS0)
          IF(0.LT.EPSZ.AND.EPSZ.LT.EPS0) W=MIN(W,EPSZ/EPS0)
        ENDIF
        DO I=1,II312
          BXYZ(I)= BXYZ(I)*W
        ENDDO
        BXYZ(3)= BXYZ(3)*W + (1.-W)*BINNER
        RETURN
      ENDIF
*
*>> Toroid part: Torroid+Solenoid field
      IF(RR.LT.RTORO.AND.AZ.LT.ZTORO.AND.NVERSION.GE.3) THEN
        IF( AVER(1:1).EQ.'X' .OR. AVER(1:1).EQ.'M' ) THEN
          CALL BINMAG( SECMAG, NRMAG,NFMAG,NZMAG, RMAG,FMAG,ZMAG,
     +                  BRMAG,BFMAG,BZMAG, 
     +DRPOS,RPOSMI,DFPOS,FPOSMI,DZPOS,ZPOSMI,JRPOS,JFPOS,JZPOS,
     +DRMAG,RMAGMI,RMAGMA,DFMAG,FMAGMI,DZMAG,ZMAGMI,JRMAG,JFMAG,JZMAG,
     +                  RFZSC,II312, BXYZMA )
          DO I=1,II312
            BXYZ(I) = - BXYZMA(I)
          ENDDO
        ELSE
          CALL BFELIX( SECFEL, NRR,NFF,NZZ, RRPOS,FFPOS,ZZPOS,
     +                  TABBR,TABBF,TABBZ, 
     +DRPOS,RPOSMI,DFPOS,FPOSMI,DZPOS,ZPOSMI,JRPOS,JFPOS,JZPOS,
     +DRMAG,RMAGMI,RMAGMA,DFMAG,FMAGMI,DZMAG,ZMAGMI,JRMAG,JFMAG,JZMAG,
     +                  RFZSC,II312, BXYZ )
          CALL BINMAG( SECMAG, NRMAG,NFMAG,NZMAG, RMAG,FMAG,ZMAG,
     +                  BRMAG,BFMAG,BZMAG, 
     +DRPOS,RPOSMI,DFPOS,FPOSMI,DZPOS,ZPOSMI,JRPOS,JFPOS,JZPOS,
     +DRMAG,RMAGMI,RMAGMA,DFMAG,FMAGMI,DZMAG,ZMAGMI,JRMAG,JFMAG,JZMAG,
     +                  RFZSC,II312, BXYZMA )
          DO I=1,II312
            BXYZ(I) = BXYZ(I) - BXYZMA(I)
          ENDDO
        ENDIF
        RETURN
      ENDIF
*
*>> Outside part: no field
      DO I=1,II312
        BXYZ(I) = 0.0
      ENDDO
*
*
      RETURN
      END
